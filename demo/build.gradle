plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
}

group 'xss.it.demo'

def appName = 'NfxIcons';
project.description='Icons Utility'

repositories {
    mavenCentral()
}

application {
    mainClass ='xss.it.demo.Demo'
    mainModule = 'nfx.icons.demo'
}


tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}



javafx {
    version = '17.0.6'
    modules = ['javafx.controls', 'javafx.fxml','javafx.graphics']
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: '../libs')
    implementation project(':icons')
}


/**
 * ======================   JLINK  =====================================================================================
 */

jlink {
    options = ['--bind-services','--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

    addExtraDependencies("javafx")
    launcher {
        name = appName
        jvmArgs = ['-XX:+UnlockExperimentalVMOptions','-XX:+UseG1GC','-XX:MaxGCPauseMillis=100','-Xms256m','-Xmx2048m' ,'-Djdk.gtk.version=2']
    }
    forceMerge('log4j-api')
    jpackage {
        installerOptions = [
                '--description', project.description,
                '--copyright', 'Copyright 2025',
                '--app-version',project.version,
                '--vendor', 'XTREME SOFTWARE SOLUTIONS (XSS&IT)'
        ] as List<String>

        //Detect OS
        def os= org.gradle.internal.os.OperatingSystem.current()
        //Windows
        if (os.isWindows()){
            imageOptions += ['--icon', 'src/main/resources/icon.ico']
            installerOptions += [
                    '--win-per-user-install', '--win-dir-chooser',
                    '--win-menu', '--win-shortcut'
            ]
        }

    }
}


