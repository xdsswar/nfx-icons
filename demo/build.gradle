plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.runtime' version '1.13.1'
}

group 'xss.it.demo'

def appName = 'NfxIcons';
project.description='Icons Utility'

repositories {
    mavenCentral()
}

application {
    mainClass ='xss.it.demo.Demo'
    applicationName = appName
}


tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}



javafx {
    version = '17.0.6'
    modules = ['javafx.controls', 'javafx.fxml','javafx.graphics']
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: '../libs')
    implementation project(':icons')
}


/**
 * ======================   JLINK  =====================================================================================
 */
runtime {
    options = ['--bind-services','--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        noConsole = false
        jvmArgs = ['-XX:+UnlockExperimentalVMOptions','-XX:+UseG1GC','-XX:MaxGCPauseMillis=100','-Xms256m','-Xmx2048m']
    }

    jpackage {
        imageName = appName
        installerName = appName
        installerOptions = [
                '--description', project.description,
                '--copyright', 'Copyright 2025',
                '--app-version',project.version,
                '--vendor', 'XTREME SOFTWARE SOLUTIONS'
        ] as List<String>

        //Detect OS
        def os= org.gradle.internal.os.OperatingSystem.current()

        //Windows
        if (os.isWindows()){
            imageOptions += ['--icon', 'src/main/resources/icon.ico']
            installerOptions += [
                    '--win-per-user-install', '--win-dir-chooser',
                    '--win-menu', '--win-shortcut'
            ]
        }
    }

}